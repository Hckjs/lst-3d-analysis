env = "lstchain-v0.9.13"
config = "config.json"


rule all:
    """Docstring comment for a rule."""
    input:
        "build/runs.json",


rule download_runlist:
    input:
        "lst1-authentication.json",
    output:
        "build/runlist.html",
    conda:
        env
    shell:
        "\
        https --check-status \
            https://lst1.iac.es/datacheck/lstosa/LST_source_catalog.html \
            --session-read-only=./{input} \
            -o {output} || rm -f {output} \
        "


rule select_datasets:
    input:
        "build/runlist.html",
        config=config,
    output:
        "build/runlist.csv",
    conda:
        env
    shell:
        "python scripts/select-data.py {input[0]} {output} -c {input.config}"


rule merge_datachecks:
    input:
        "build/runlist.csv",
        config=config,
    output:
        "build/dl1-datachecks.h5",
    conda:
        env
    shell:
        "python scripts/merge-datachecks.py {input[0]} {output} -c {input.config}"


rule run_ids:
    output:
        "build/runs.json",
    input:
        "build/runlist-checked.csv",
        config=config,
    conda:
        env
    shell:
        "python scripts/create-night-run-list.py {input[0]} {output} -c {input.config}"


rule data_check:
    input:
        "build/runlist.csv",
        "build/dl1-datachecks.h5",
        config=config,
    output:
        "build/runlist-checked.csv",
    conda:
        env
    shell:
        "\
        python \
            scripts/data-check.py \
            {input[0]} \
            {input[1]} \
            --output {output} \
            --config {input.config} \
        "


rule authentication:
    output:
        "lst1-authentication.json",
    shell:
        "echo 'Provide the file {output} as explained in README.md'"
