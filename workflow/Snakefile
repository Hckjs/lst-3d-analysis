# vim: ft=snakemake nofoldenable commentstring=#%s
# https://github.com/snakemake/snakemake/tree/main/misc/vim

import json
from itertools import chain

with open("build/runs.json", "r") as f:
    runs = json.load(f)

RUN_IDS = sorted(set(chain(*runs.values())))


lstchain_env = "lstchain-v0.9.13"
gammapy_env = "envs/environment.yml"

plots = [
    "build/plots/mrk421/theta2.pdf",
    "build/plots/mrk421/flux_points.pdf",
    "build/plots/mrk421/light_curve.pdf",
    "build/plots/mrk421/observation_plots.pdf",
    # "build/plots/irf/edisp.pdf",
    # "build/plots/irf/gh_cut.pdf",
    # "build/plots/irf/radmax_cut.pdf",
]
irf_plots = [
    f"build/plots/mrk421/irf/{irf}_Run{run_id:05d}.pdf"
    for irf in ("aeff", "edisp", "gh_cut", "radmax_cut")
    for run_id in RUN_IDS
]


rule all:
    input:
        plots,
        irf_plots,


localrules:
    plot_irf,


rule plot_irf:
    output:
        "build/plots/{source}/irf/{irf}_Run{run_id}.pdf",
    input:
        data="build/irf/calculated/{source}/irf_Run{run_id}.fits.gz",
        script="scripts/plot_irf_{irf}.py",
    resources:
        mem_mb=1000,
        time=5,  # minutes
    conda:
        gammapy_env
    shell:
        "python {input.script} -i {input.data} -o {output}"


rule calc_theta2:
    output:
        "build/dl4/{source}/theta2.fits.gz",
    input:
        data="build/dl3/{source}/hdu-index.fits.gz",
        config="configs/{source}.yaml",
        script="scripts/calc_theta2.py",
    resources:
        mem_mb=16000,
    conda:
        gammapy_env
    shell:
        "python {input.script} -c {input.config} -o {output}"


rule calc_flux_points:
    input:
        data="build/dl4/{source}/datasets.fits.gz",
        config="configs/{source}.yaml",
        model="build/dl4/{source}/model-best-fit.yaml",
        script="scripts/calc_flux_points.py",
    output:
        "build/dl4/{source}/flux_points.fits.gz",
    conda:
        gammapy_env
    shell:
        """
        python {input.script} \
            -c {input.config} \
            --dataset-path {input.data} \
            --best-model-path {input.model} \
            -o {output}
        """


ruleorder: plot_flux_points > plot


rule plot_flux_points:
    input:
        data="build/dl4/{source}/flux_points.fits.gz",
        model="build/dl4/{source}/model-best-fit.yaml",
        script="scripts/plot_flux_points.py",
    output:
        "build/plots/{source}/flux_points.pdf",
    conda:
        gammapy_env
    shell:
        """
        python {input.script} \
            -i {input.data} \
            --best-model-path {input.model} \
            -o {output}
        """


rule plot:
    output:
        "build/plots/{source}/{name}.pdf",
    input:
        data="build/dl4/{source}/{name}.fits.gz",
        script="scripts/plot_{name}.py",
    conda:
        gammapy_env
    shell:
        "python {input.script} -i {input.data} -o {output}"


rule observation_plots:
    input:
        "build/dl3/{source}/hdu-index.fits.gz",
        config="configs/{source}.yaml",
        script="scripts/events.py",
    output:
        "build/plots/{source}/observation_plots.pdf",
    resources:
        mem_mb=64000,
    conda:
        gammapy_env
    shell:
        """
        python {input.script} \
            -c {input.config} \
            -o {output} \
        """


rule sensitivity:
    input:
        "build/model-best-fit.yaml",
        "build/dl3/hdu-index.fits.gz",
        script="scripts/sensitivity.py",
    output:
        "build/plots/sensitivity.pdf",
    conda:
        gammapy_env
    shell:
        "python {input.script} -o {output}"


rule calc_light_curve:
    input:
        model="build/dl4/{source}/model-best-fit.yaml",
        config="configs/{source}.yaml",
        dataset="build/dl4/{source}/datasets.fits.gz",
        script="scripts/calc_light_curve.py",
    output:
        "build/dl4/{source}/light_curve.fits.gz",
    conda:
        gammapy_env
    shell:
        """
        python {input.script} \
            -c {input.config} \
            --dataset-path {input.dataset} \
            --best-model-path {input.model} \
            -o {output} \
        """


rule model_best_fit:
    input:
        config="configs/{source}.yaml",
        dataset="build/dl4/{source}/datasets.fits.gz",
        model="configs/model-{source}.yaml",
        script="scripts/fit-model.py",
    output:
        "build/dl4/{source}/model-best-fit.yaml",
    conda:
        gammapy_env
    shell:
        """
        python {input.script} \
            -c {input.config} \
            --dataset-path {input.dataset} \
            --model-config {input.model} \
            -o {output} \
        """


rule dataset:
    input:
        data="build/dl3/{source}/hdu-index.fits.gz",
        config="configs/{source}.yaml",
        script="scripts/write_datasets.py",
    output:
        "build/dl4/{source}/datasets.fits.gz",
    resources:
        cpus=16,
        mem_mb="32G",
        time=30,  # minutes
    conda:
        gammapy_env
    shell:
        "python {input.script} -j0 -c {input.config} -o {output} --n-off-regions=5"


rule dl3_hdu_index:
    conda:
        lstchain_env
    output:
        "build/dl3/{source}/hdu-index.fits.gz",
    input:
        expand(
            "build/dl3/{source}/dl3_LST-1.Run{run_id:05d}.fits",
            run_id=RUN_IDS,
            source=["mrk421"],
        ),
    resources:
        time=15,
    shell:
        """
        lstchain_create_dl3_index_files  \
            --input-dl3-dir build/dl3/{wildcards.source}  \
            --output-index-path build/dl3/{wildcards.source}  \
            --file-pattern 'dl3_*.fits'  \
            --overwrite \
        """


# rule select_low_offset:
#     output:
#         "build/dl3/{source}/dl3_LST-1.Run{run_id}.fits.gz",
#     input:
#         "build/dl3/{source}/dl3_LST-1.Run{run_id}.fits",
#     conda:
#         gammapy_env
#     resources:
#         time=10,
#     log:
#         "build/logs/dl3/{run_id}-{source}.log",
#     shell:
#         """
#         python scripts/select_low_offset.py \
#             -i {input} -o {output} \
#         """


rule dl3:
    output:
        "build/dl3/{source}/dl3_LST-1.Run{run_id}.fits",
    input:
        data="build/dl2/{source}/dl2_LST-1.Run{run_id}.h5",
        irf="build/irf/calculated/{source}/irf_Run{run_id}.fits.gz",
        config="configs/irf_tool_config.json",
    resources:
        mem_mb="12G",
        time=30,
    conda:
        lstchain_env
    log:
        "build/logs/dl3/{source}/{run_id}.log",
    shell:
        """
        lstchain_create_dl3_file  \
            --input-dl2 {input.data}  \
            --output-dl3-path $(dirname $(realpath {output}))  \
            --input-irf {input.irf}  \
            --source-name {wildcards.source}  \
            --config {input.config} \
            --overwrite \
        """


rule dl2:
    resources:
        mem_mb="64G",
    output:
        "build/dl2/{source}/dl2_LST-1.Run{run_id}.h5",
    input:
        data="build/dl1/{source}/dl1_LST-1.Run{run_id}.h5",
        config="build/models/{source}/model_Run{run_id}/lstchain_config.json",
    conda:
        lstchain_env
    log:
        "build/logs/dl2/{run_id}-{source}.log",
    shell:
        """
        lstchain_dl1_to_dl2  \
            --input-file {input.data}  \
            --output-dir $(dirname $(realpath {output}))  \
            --path-models $(dirname {input.config})  \
            --config {input.config}  \
        """


rule irf:
    resources:
        mem_mb="8G",
        time=10,
    output:
        "build/irf/calculated/{source}/irf_Run{run_id}.fits.gz",
    input:
        gammas="build/dl2/test/{source}/dl2_LST-1.Run{run_id}.h5",
        config="configs/irf_tool_config.json",
    conda:
        lstchain_env
    shell:
        """
        lstchain_create_irf_files \
            -o {output} \
            -g {input.gammas} \
            --config {input.config} \
        """
